plugins {
    id "com.android.application"
    // START: FlutterFire Configuration
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    // END: FlutterFire Configuration
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

dependencies {
    implementation platform('com.google.firebase:firebase-bom:33.16.0')
    implementation 'com.google.android.play:app-update:2.1.0'
    implementation 'com.google.android.play:app-update-ktx:2.1.0'
}

android {
    namespace = "sk.anickaapavol.organista"
    compileSdk = Math.max(35, flutter.compileSdkVersion)
    // After updating Flutter to 3.32.5, the ndkVersion is not set correctly, needs to be set manually
    // ndkVersion = flutter.ndkVersion
    ndkVersion = "27.0.12077973"

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    defaultConfig {
        applicationId = "sk.anickaapavol.organista"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdkVersion = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
        manifestPlaceholders = [
            'enableDeferredComponents': 'false',
            'applicationName': 'io.flutter.app.FlutterApplication'
        ]
        
        // Enable native debug symbols
        ndk {
            debugSymbolLevel 'FULL'
        }
    }

    signingConfigs {
        release {
            def keystorePath = System.getenv("ANDROID_KEYSTORE_FILE")
            def keystorePassword = System.getenv("ANDROID_KEYSTORE_PASSWORD")
            def keyAlias = System.getenv("ANDROID_KEY_ALIAS")
            def keyPassword = System.getenv("ANDROID_KEY_PASSWORD")
            
            // Only configure signing if all required values are present (local builds)
            // On Codemagic with native signing, these will be null and Codemagic handles signing
            if (keystorePath != null && keystorePassword != null && 
                keyAlias != null && keyPassword != null) {
                storeFile file(keystorePath)
                storePassword keystorePassword
                this.keyAlias = keyAlias
                this.keyPassword = keyPassword
            }
        }
    }

    buildTypes {
        release {
            // Only apply signing config if it's properly configured (local builds)
            // On Codemagic with native signing, this will be null and Codemagic handles signing
            if (signingConfigs.release.storeFile != null) {
                signingConfig signingConfigs.release
            }
            // Enables code shrinking, obfuscation, and optimization
            minifyEnabled true
            // Enables resource shrinking
            shrinkResources true
            // Specifies the ProGuard rules file
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            
            // Enable native debug symbols for release builds
            ndk {
                debugSymbolLevel 'FULL'
            }
        }
    }
}

flutter {
    source = "../.."
}
