plugins {
    id "com.android.application"
    // START: FlutterFire Configuration
    id 'com.google.gms.google-services'
    id "io.sentry.android.gradle" version "5.12.2"
    // END: FlutterFire Configuration
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

dependencies {
    implementation platform('com.google.firebase:firebase-bom:33.16.0')
    implementation 'com.google.android.play:app-update:2.1.0'
    implementation 'com.google.android.play:app-update-ktx:2.1.0'
}

android {
    namespace = "sk.anickaapavol.organista"
    compileSdk = Math.max(35, flutter.compileSdkVersion)
    ndkVersion = flutter.ndkVersion

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    defaultConfig {
        applicationId = "sk.anickaapavol.organista"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdkVersion = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
        manifestPlaceholders = [
            'enableDeferredComponents': 'false',
            'applicationName': 'io.flutter.app.FlutterApplication'
        ]
        
        // Enable native debug symbols
        ndk {
            debugSymbolLevel 'FULL'
        }
    }

    signingConfigs {
        debug {
            def keyPropsFile = file("keystore/debug_signing.properties")
            if (keyPropsFile.exists()) {
                def keyProps = new Properties()
                keyProps.load(new FileInputStream(keyPropsFile))
                storeFile file(keyProps['storeFile'])
                storePassword keyProps['storePassword']
                keyAlias keyProps['keyAlias']
                keyPassword keyProps['keyPassword']
            }
        }
        release {
            def keyPropsFile = file("keystore/release_signing.properties")
            if (keyPropsFile.exists()) {
                def keyProps = new Properties()
                keyProps.load(new FileInputStream(keyPropsFile))
                storeFile file(keyProps['storeFile'])
                storePassword keyProps['storePassword']
                keyAlias keyProps['keyAlias']
                keyPassword keyProps['keyPassword']
            } else if (System.getenv("CM_KEYSTORE_PATH") != null) {
                storeFile file(System.getenv("CM_KEYSTORE_PATH"))
                storePassword System.getenv("CM_KEYSTORE_PASSWORD")
                keyAlias System.getenv("CM_KEY_ALIAS")
                keyPassword System.getenv("CM_KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
            // Enables code shrinking, obfuscation, and optimization
            minifyEnabled true
            // Enables resource shrinking
            shrinkResources true
            // Specifies the ProGuard rules file
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // Enable native debug symbols for release builds
            ndk {
                debugSymbolLevel 'FULL'
            }
        }
    }
}

flutter {
    source = "../.."
}

sentry {
    autoInstallation {
        enabled = false
    }
    // Disable automatic upload of ProGuard/R8 mappings - handled by sentry_dart_plugin
    autoUploadProguardMapping = false
    // Disable source context upload - handled by sentry_dart_plugin
    includeSourceContext = false
}
